#Использовать asserts
#Использовать logos
#Использовать 1connector

Перем Лог;

#Область ПрограммныйИнтерфейс

// Возвращает приоритет провайдера
//
//  Возвращаемое значение:
//   Число - текущий приоритет провайдера
//
Функция Приоритет() Экспорт
	Возврат 1;
КонецФункции

// Возвращает идентификатор провайдера
//
//  Возвращаемое значение:
//   Строка - текущий идентификатор провайдера
//
Функция Идентификатор() Экспорт
	Возврат "vault";
КонецФункции

// Возвращает тип провайдера
//
//  Возвращаемое значение:
//   Строка - текущий тип провайдера
//
Функция ТипПровайдера() Экспорт
	Возврат "vault";
КонецФункции

// Выполняет чтение параметров для провайдера
//
// Параметры:
//   НастройкиПровайдера - Структура - структура настроек провайдера
//
//  Возвращаемое значение:
//   Соответствие - результат чтения провайдера
//
Функция ПрочитатьПараметры(Знач НастройкиПровайдера) Экспорт

	ПрочитанныеПараметры = Новый Соответствие;

	Адрес = НастройкиПровайдера.Адрес;
	Токен = НастройкиПровайдера.Токен;
	Запрос = НастройкиПровайдера.Запрос;

	Если Не ЗначениеЗаполнено(Адрес) Тогда
		Лог.Отладка("Не указан адрес сервера. Чтение невозможно");
		Возврат ПрочитанныеПараметры;
	КонецЕсли;

	Лог.Отладка("Выполняю чтение данных vault из <%1>/%2", Адрес, Запрос);
	ПрочитанныеПараметры = Прочитать(Адрес, Токен, Запрос);

	Возврат ПрочитанныеПараметры;
КонецФункции

#КонецОбласти

// Выполнить чтение настроек из vault
//
// Параметры:
//   Адрес - Cтрока - URL сервера vault
//   Токен - Cтрока - токен авторизации
//   Запрос - Cтрока - путь  к данным vault
// Возвращаемое значение:
//   Соответствие - итоговые параметры
//
Функция Прочитать(Знач Адрес, Знач Токен, Знач Запрос)

	Настройки = Новый Соответствие;
	
	URL = СтрШаблон("%1/%2", СтрУбратьСКонца(Адрес, "/"), Запрос);
	Заголовки = Новый Соответствие;
		
	Если ЗначениеЗаполнено(Токен) Тогда
		Заголовки.Вставить("Authorization", СтрШаблон("Bearer %1", Токен));
	КонецЕсли;

	ДопПараметры = Новый Структура("Заголовки", Заголовки);
	
	Попытка

		Ответ = КоннекторHTTP.Get(URL, , ДопПараметры);
		ВолтОтветил = ВолтОтветил(Ответ);
		Если Не ВолтОтветил Тогда
			Лог.Отладка("Ошибка получения данных из vault:
			|%1", КодыСостоянияHTTP.Представление(Ответ.КодСостояния));
		КонецЕсли;

		ДесериализованныйОтвет = Ответ.Json();
		Ошибки = ДесериализованныйОтвет.Получить("errors");
		Если Не Ошибки = Неопределено Тогда
			Для Каждого Ошибка Из Ошибки Цикл
				Лог.Отладка(Ошибка["message"]);
			КонецЦикла;
		КонецЕсли;

		Настройки = ?(ВолтОтветил, ДесериализованныйОтвет["data"], Новый Соответствие);

		Лог.Отладка("Итоговые параметры:");
		ПоказатьПараметрыВРежимеОтладки(Настройки);

	Исключение

		Лог.Отладка("Ошибка чтения настроек vault из %1
		|%2", URL, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));

		ВызватьИсключение;

	КонецПопытки;

	Возврат Настройки;
КонецФункции

Функция ВолтОтветил(Знач Ответ)

	ХорошиеКоды = Новый Массив;
	ХорошиеКоды.Добавить(200);
	ХорошиеКоды.Добавить(204);

	Возврат Не ХорошиеКоды.Найти(Ответ.КодСостояния) = Неопределено;
КонецФункции

Процедура ПоказатьПараметрыВРежимеОтладки(ЗначенияПараметров)

	ПроцессорВывода = Новый ВыводВРежимеОтладки(Лог);
	ПроцессорВывода.ПоказатьНастройкиВРежимеОтладки(ЗначенияПараметров);

КонецПроцедуры

Функция СтрУбратьСКонца(Знач Стр, Знач СтрПоиска)
	Возврат ?(СтрЗаканчиваетсяНа(Стр, СтрПоиска), Лев(Стр, СтрДлина(Стр) - СтрДлина(СтрПоиска)), Стр);
КонецФункции

Лог = Логирование.ПолучитьЛог("oscript.lib.configor.vault");
