#Использовать "../src"
#Использовать asserts
#Использовать 1connector
#Использовать 1commands

Перем ПроцессВина;
Перем АдресВина;

&ПередКаждым
Процедура ЗапуститьВино() Экспорт
	ЗапуститьТестовыйВолт();
КонецПроцедуры

&ПослеКаждого
Процедура ОстановитьВино() Экспорт
	УбитьТестовыйВолт();
КонецПроцедуры

&Тест
Процедура ПроверитьПолучениеПараметровИзVault() Экспорт

	// Дано
	НастройкиVault = Новый Структура;
    НастройкиVault.Вставить("Адрес", АдресВина);
    НастройкиVault.Вставить("Токен", "VAULT_TOKEN");
    НастройкиVault.Вставить("Запрос", "v1/my_secret_store/super_secret");

    МенеджерПараметров = Новый МенеджерПараметров();
	МенеджерПараметров.ДобавитьПровайдерПараметров(Новый ПровайдерПараметровVAULT());
	МенеджерПараметров.УстановитьНастройкиVault(НастройкиVault);

	// Когда
	МенеджерПараметров.Прочитать();

	// Тогда
	Ожидаем.Что(МенеджерПараметров.Параметр("owner.contact.email")).Равно("");
	Ожидаем.Что(МенеджерПараметров.Параметр("owner.contact.tel")).Равно("77777777");
	Ожидаем.Что(МенеджерПараметров.Параметр("owner.person.address")).Равно("Alpha Centauri");
	Ожидаем.Что(МенеджерПараметров.Параметр("admin.contact.email")).Равно("admin.ru");
	Ожидаем.Что(МенеджерПараметров.Параметр("person.address")).ЭтоНеопределено();

КонецПроцедуры

Функция КаталогFixtures()
	Возврат ОбъединитьПути(КаталогБиблиотеки(), "tests", "fixtures");
КонецФункции

Функция КаталогБиблиотеки()
	Возврат ОбъединитьПути(ТекущийСценарий().Каталог, "..");
КонецФункции

Процедура ЗапуститьТестовыйВолт()

	Путь = ОбъединитьПути(КаталогFixtures(), "ТестовыйVault");

	ЗапускВина = Новый Команда;
	ЗапускВина.УстановитьСтрокуЗапуска("oscript main.os");
	ЗапускВина.УстановитьРабочийКаталог(Путь);

	ПроцессВина = ЗапускВина.ЗапуститьПроцесс();

	Для Сч = 0 По 10 Цикл

		Приостановить(1000);

		Попытка

			Ответ = КоннекторHTTP.Get(
				СтрШаблон("%1/v1/ping", АдресВина), ,
				Новый Структура("Заголовки", Новый Соответствие)
			).Текст();

			Если Ответ = "ОК" Тогда
				Прервать;
			КонецЕсли;

		Исключение
			Продолжить;
		КонецПопытки;

	КонецЦикла;

КонецПроцедуры

Процедура УбитьТестовыйВолт()

	КоннекторHTTP.Get(
		СтрШаблон("%1/v1/kill", АдресВина), ,
		Новый Структура("Заголовки", Новый Соответствие)
	);

	ПроцессВина.Завершить();

КонецПроцедуры

АдресВина = "http://localhost:3333";
