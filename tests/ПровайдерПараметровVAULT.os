#использовать "../src/"
#Использовать asserts
#Использовать 1connector

&Тест
Процедура ПроверитьПолучениеПараметровИзVault() Экспорт

	ЗапуститьТестовыйВолт(ОбъединитьПути(КаталогFixtures(), "ТестовыйVault", "main.os"));

	// Дано

	НастройкиVault = Новый Структура;
    НастройкиVault.Вставить("Адрес", "http://localhost:3333");
    НастройкиVault.Вставить("Токен", "VAULT_TOKEN");
    НастройкиVault.Вставить("Запрос", "v1/my_secret_store/super_secret");

    МенеджерПараметров = Новый МенеджерПараметров();
	МенеджерПараметров.ДобавитьПровайдерПараметров(Новый ПровайдерПараметровVAULT());
	МенеджерПараметров.УстановитьНастройкиVault(НастройкиVault);

	// Когда

	МенеджерПараметров.Прочитать();

	// Тогда

	Ожидаем.Что(МенеджерПараметров.Параметр("owner.contact.email")).Равно("");
	Ожидаем.Что(МенеджерПараметров.Параметр("owner.contact.tel")).Равно("77777777");
	Ожидаем.Что(МенеджерПараметров.Параметр("owner.person.address")).Равно("Alpha Centauri");
	Ожидаем.Что(МенеджерПараметров.Параметр("admin.contact.email")).Равно("admin.ru");
	Ожидаем.Что(МенеджерПараметров.Параметр("person.address")).ЭтоНеопределено();

	УбитьТестовыйВолт(НастройкиVault["Адрес"]);

КонецПроцедуры

Функция КаталогFixtures()
	Возврат ОбъединитьПути(КаталогБиблиотеки(), "tests", "fixtures");
КонецФункции

Функция КаталогБиблиотеки()
	Возврат ОбъединитьПути(ТекущийСценарий().Каталог, "..");
КонецФункции

Функция ЗапуститьТестовыйВолт(Знач Путь)
	ФоновоеДляВина = Новый Массив;
	ФоновоеДляВина.Добавить(Путь);

	ТекущийКаталог = ТекущийКаталог();
	УстановитьТекущийКаталог(Новый Файл(Путь).Путь);
	ФЗ = ФоновыеЗадания.Выполнить(ЭтотОбъект, "ЗапускВина", ФоновоеДляВина);
	УстановитьТекущийКаталог(ТекущийКаталог);

	Возврат ФЗ;
КонецФункции

Функция ЗапускВина(Знач Путь) Экспорт
	ЗагрузитьСценарий(Путь);
	Возврат Истина;
КонецФункции

Функция УбитьТестовыйВолт(Знач Адрес)
	URL = СтрШаблон("%1/v1/kill", Адрес);
	Возврат КоннекторHTTP.Get(URL, , Новый Структура("Заголовки", Новый Соответствие));
КонецФункции
